# -*- mode: autoconf -*-
# Configure macros for cedar
#
# Code for version detection and comparison comes from freetype2.m4
# Marcelo Magallon 2001-10-26, based on gtk.m4 by Owen Taylor
#
# Copyright 2001, 2003 by
# David Turner, Robert Wilhelm, and Werner Lemberg.
#
# This file is part of the FreeType project, and may only be used, modified,
# and distributed under the terms of the FreeType project license,
# LICENSE.TXT.  By continuing to use, modify, or distribute this file you
# indicate that you have read the license and understand and accept it
# fully.
#
# As a special exception to the FreeType project license, this file may be
# distributed as part of a program that contains a configuration script
# generated by Autoconf, under the same distribution terms as the rest of
# that program.
#

# AC_CHECK_CEDAR([MINIMUM-VERSION [, ACTION-IF-FOUND [, ACTION-IF-NOT-FOUND]]])
# Test for CEDAR Library and define CEDAR_CFLAGS and CEDAR_LIBS.
# Check that the version is above MINIMUM-VERSION 
# use when linking with a c++ aware linker, with a c linker you may also
# need -lstdc++

AC_DEFUN([AC_CHECK_CEDAR],
[
  cedar_min_version=m4_if([$1], [], [3.5.0], [$1])
  cedar_no=
  cedar_pkgconfig=yes 
  PKG_CHECK_MODULES([CEDAR],[cedar >= $cedar_min_version],,
    [cedar_pkgconfig=no])
  
  if test $cedar_pkgconfig = no; then
    AC_ARG_WITH(cedar,[  --with-cedar=PFX   Prefix where CEDAR library is installed],
		cedar_config_prefix="$withval", cedar_config_prefix="")

    if test x$cedar_config_prefix != x ; then
        if test x${CEDAR_CONFIG+set} != xset ; then
	    CEDAR_CONFIG=$cedar_config_prefix/bin/cedar-config
        fi
    fi

    AC_PATH_PROG([CEDAR_CONFIG], [cedar-config], [no])
    if test "$CEDAR_CONFIG" = "no" ; then
      cedar_no=yes
    else
      cedar_config_major_version=`$CEDAR_CONFIG --version | sed 's/^cedar \([[0-9]][[0-9]]*\)\.\([[0-9]][[0-9]]*\)\.\([[0-9]][[0-9]]*\)[[ab]]*$/\1/'`
      cedar_config_minor_version=`$CEDAR_CONFIG --version | sed 's/^cedar \([[0-9]][[0-9]]*\)\.\([[0-9]][[0-9]]*\)\.\([[0-9]][[0-9]]*\)[[ab]]*$/\2/'`
      cedar_config_micro_version=`$CEDAR_CONFIG --version | sed 's/^cedar \([[0-9]][[0-9]]*\)\.\([[0-9]][[0-9]]*\)\.\([[0-9]][[0-9]]*\)[[ab]]*$/\3/'`
      cedar_min_major_version=`echo $cedar_min_version | sed 's/\([[0-9]][[0-9]]*\).\([[0-9]][[0-9]]*\).\([[0-9]][[0-9]]*\)$/\1/'`
      cedar_min_minor_version=`echo $cedar_min_version | sed 's/\([[0-9]][[0-9]]*\).\([[0-9]][[0-9]]*\).\([[0-9]][[0-9]]*\)$/\2/'`
      cedar_min_micro_version=`echo $cedar_min_version | sed 's/\([[0-9]][[0-9]]*\).\([[0-9]][[0-9]]*\).\([[0-9]][[0-9]]*\)$/\3/'`

      cedar_config_is_lt=""
      if test $cedar_config_major_version -lt $cedar_min_major_version ; then
        cedar_config_is_lt=yes
      else
        if test $cedar_config_major_version -eq $cedar_min_major_version ; then
          if test $cedar_config_minor_version -lt $cedar_min_minor_version ; then
            cedar_config_is_lt=yes
          else
            if test $cedar_config_minor_version -eq $cedar_min_minor_version ; then
              if test $cedar_config_micro_version -lt $cedar_min_micro_version ; then
                cedar_config_is_lt=yes
              fi
            fi
          fi
        fi
      fi
      if test x$cedar_config_is_lt = xyes ; then
        cedar_no=yes
      else
        CEDAR_LIBS="`$CEDAR_CONFIG --libs`"
        CEDAR_CFLAGS="`$CEDAR_CONFIG --cflags`"
      fi
    fi
  else
     CEDAR_STATIC_LIBS="`$PKG_CONFIG --static --libs Cedar`"
  fi
  AC_MSG_CHECKING([for cedar version >= $cedar_min_version])
  if test x$cedar_no = x ; then
    AC_MSG_RESULT([yes])
    m4_if([$2], [], [:], [$2])
  else
    AC_MSG_RESULT([no])
    if test "$CEDAR_CONFIG" = "no" ; then
    AC_MSG_NOTICE([The cedar-config script could not be found.])
    else
      if test x$cedar_config_is_lt = xyes ; then
        AC_MSG_NOTICE([the installed cedar library is too old.])
      fi
    fi
    CEDAR_LIBS=""
    CEDAR_CFLAGS=""
    m4_if([$3], [], [:], [$3])
  fi
  if test x"$CEDAR_STATIC_LIBS" = x ; then
    CEDAR_STATIC_LIBS=$CEDAR_LIBS
  fi
  AC_SUBST([CEDAR_CFLAGS])
  AC_SUBST([CEDAR_LIBS])
  AC_SUBST([CEDAR_STATIC_LIBS])
]) 

